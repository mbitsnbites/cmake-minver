cmake_minimum_required(VERSION 2.8.5)

# Original program by Florian: http://stackoverflow.com/a/40854673/5778708

function(version_required_by)
    set(_keywords ${ARGN})
    foreach(_ver IN ITEMS 2.6 2.8.0 2.8.1 2.8.2 2.8.3 2.8.4 2.8.5 2.8.6 2.8.7
                          2.8.8 2.8.9 2.8.10 2.8.11 2.8.12 3.0 3.1 3.2 3.3 3.4
                          3.5 3.6 3.7 3.8 3.9 3.10 3.11 3.12)
        if (_ver VERSION_LESS 2.8)
            set(_url "https://cmake.org/cmake/help/cmake${_ver}docs.html")
        elseif (_ver VERSION_LESS 3.0)
            set(_url "https://cmake.org/cmake/help/v${_ver}/cmake.html")
        else()
            set(_url "https://cmake.org/cmake/help/latest/release/${_ver}.html")
        endif()
        set(_temp_file "${CMAKE_BINARY_DIR}/cache/${_ver}.html")
        if (NOT EXISTS "${_temp_file}")
            message(STATUS "Download ${_url}")
            file(DOWNLOAD "${_url}" "${_temp_file}")
        endif()
        file(READ "${_temp_file}" _help_text)
        foreach(_keyword IN LISTS _keywords)
            string(FIND "${_help_text}" "${_keyword}" _found)
            if (NOT _found EQUAL -1)
                message(STATUS "Found: ${_keyword}\n\tv${_ver}\n\t${_url}")
                list(REMOVE_ITEM _keywords "${_keyword}")
            endif()
        endforeach()
        if (NOT _keywords)
            message(STATUS "Done.")
            message("\ncmake_minimum_required(VERSION ${_ver} FATAL_ERROR)\n")
            break()
        endif()
    endforeach()
    if (_keywords)
        message(FATAL_ERROR "Not found ${_keywords}")
    endif()
endfunction()

if (CMAKE_SCRIPT_MODE_FILE AND (${CMAKE_ARGC} GREATER 3))
    foreach(_i RANGE 3 ${CMAKE_ARGC})
        list(APPEND _args "${CMAKE_ARGV${_i}}")
    endforeach()
else()
    message(FATAL_ERROR "Usage: cmake -P ${CMAKE_CURRENT_LIST_FILE} keyword")
endif()
version_required_by(${_args})
